import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
  alias libs.plugins.android.library
  alias libs.plugins.kotlin.multiplatform
  alias libs.plugins.kotlin.serialization
  alias libs.plugins.detekt
}

android {
  compileSdk libs.versions.targetSdk.get().toInteger()
  namespace 'net.rafaeltoledo.reddit'

  defaultConfig {
    multiDexEnabled true
    minSdk libs.versions.minSdk.get().toInteger()
  }

  compileOptions {
    coreLibraryDesugaringEnabled true

    sourceCompatibility JavaVersion.VERSION_11
    targetCompatibility JavaVersion.VERSION_11
  }
}

dependencies {
  coreLibraryDesugaring libs.android.tools.jdk.desugar
}

kotlin {
  android()

  def os = DefaultNativePlatform.currentOperatingSystem
  def architecture = DefaultNativePlatform.currentArchitecture
  if (os.macOsX) {
    ios {
      binaries {
        framework {
          baseName = project.name.capitalize()
        }
      }
    }

    if (architecture.isArm()) {
      iosSimulatorArm64 {
        binaries {
          framework {
            baseName = project.name.capitalize()
          }
        }
      }

      sourceSets {
        iosSimulatorArm64Main {
          dependsOn iosMain
        }
        iosSimulatorArm64Test {
          dependsOn iosTest
        }
      }
    }
  }

  sourceSets {
    commonMain {
      dependencies {
        implementation libs.kotlinx.serialization.json
        implementation libs.ktor.client.contentNegotiation
        implementation libs.ktor.client.core
        implementation libs.ktor.serialization.kotlinx.json
      }
    }

    androidMain {
      dependencies {
        implementation libs.ktor.client.android
      }
    }

    iosMain {
      dependencies {
        implementation libs.ktor.client.ios
      }
    }
  }

  jvmToolchain(11)
}

dependencies {
  detektPlugins libs.detekt.compose.rules
}

detekt {
  source = files(
    'src/commonMain/kotlin',
    'src/androidMain/kotlin',
    'src/iosMain/kotlin',
  )

  config = files("$rootDir/config/detekt/detekt.yml")
}
