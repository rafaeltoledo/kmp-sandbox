plugins {
  alias libs.plugins.android.application
  alias libs.plugins.hilt.android
  alias libs.plugins.kotlin.android
  alias libs.plugins.kotlin.kapt
  alias libs.plugins.detekt
}

android {
  compileSdk libs.versions.targetSdk.get().toInteger()
  namespace 'net.rafaeltoledo.reddit.android'

  defaultConfig {
    targetSdk libs.versions.targetSdk.get().toInteger()
    minSdk libs.versions.minSdk.get().toInteger()
  }

  signingConfigs {
    debug {
      storeFile file("$rootDir/distribution/debug.keystore")
    }

    release {
      storeFile file("$rootDir/distribution/release.keystore")
      storePassword System.env.ANDROID_RELEASE_STORE_PASSWORD
      keyAlias 'kmp'
      keyPassword System.env.ANDROID_RELEASE_KEY_PASSWORD
    }
  }

  buildTypes {
    release {
      signingConfig signingConfigs.release

      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }

  buildFeatures {
    compose true
  }

  composeOptions {
    kotlinCompilerExtensionVersion = libs.versions.androidx.compose.compiler.get()
  }
}

kotlin {
  jvmToolchain(17)
}

kapt {
  correctErrorTypes true
}

hilt {
  enableAggregatingTask = true
}

dependencies {
  implementation project(':shared')

  implementation libs.androidx.activity.compose
  implementation libs.androidx.core.splashscreen
  implementation libs.androidx.navigation.compose
  implementation libs.androidx.lifecycle.runtime.compose
  implementation libs.androidx.lifecycle.viewmodel.compose

  implementation platform(libs.androidx.compose.bom)

  implementation libs.androidx.compose.material.material
  implementation libs.androidx.compose.material3

  implementation libs.androidx.hilt.navigation.compose

  implementation libs.hilt.android
  kapt libs.hilt.compiler

  detektPlugins libs.detekt.compose.rules
}

detekt {
  config.setFrom("$rootDir/config/detekt/detekt.yml")
}
